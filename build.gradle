import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    apply from: "gitflow.gradle", to: buildscript
    apply from: "docker.gradle", to: buildscript
}

plugins {
    id 'net.foragerr.jmeter' version '1.0.10-3.3'
    id 'org.springframework.boot' version '1.5.9.RELEASE'
    id "org.sonarqube" version "2.6.2"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'io.github.robwin.jgitflow'
apply plugin: 'com.bmuschko.docker-remote-api'

def artifactId = rootProject.name
group = project.property("project.group")
version = project.property("project.version")

def nexusUrl = project.property("nexus.url")
def dockerRegistryUrl = project.property("docker.registry.url")

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Jar) {
    destinationDir = file("$rootDir/build/resources/main/lib")
}


sourceSets {
    main {
        resources {
            srcDirs "src/main/resources", "src/main/ci"
        }
    }
    test {
        resources {
            srcDirs "src/test/resources", "src/test/jmeter"
        }
    }
}

processResources {
    filter ReplaceTokens, tokens: [
            "project.version"    : version,
            "project.group"      : project.property("project.group"),
            "project.artifactId" : artifactId,
            "docker.registry.url": project.property("docker.registry.url"),
            "jarFileLocation"    : buildDir.name
    ]
}

processTestResources {
    filter ReplaceTokens, tokens: [
            "jmeter.remoteServiceHost": project.property("jmeter.remoteServiceHost"),
            "jmeter.renoteServiceHost": project.property("jmeter.remoteServicePort")
    ]
}

repositories {
    maven { url "$nexusUrl/repository/maven-central" }
    maven { url "$nexusUrl/repository/releases" }
    maven { url "$nexusUrl/repository/snapshots" }
    jcenter()
}


dependencies {
    implementation 'com.stratio.architecture:stratio-microservices-parent:0.4.0'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.projectlombok:lombok'
    implementation 'commons-io:commons-io'
    implementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'info.cukes:cucumber-core'
    implementation 'info.cukes:cucumber-java'
    implementation 'info.cukes:cucumber-junit'
    implementation 'info.cukes:cucumber-spring'
}

task unitTest(type: Test) {
    exclude '**/integration/**'
}

task integrationTest(type: Test) {
    include '**/integration/**'
}

task buildDockerImage(type: DockerBuildImage) {
    inputDir = file('build/resources/main')
    tag = "$dockerRegistryUrl/$artifactId:$version"
}

task pushDockerImage(type: DockerPushImage) {
    tag = version
    imageName = "$dockerRegistryUrl/$artifactId"
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "$nexusUrl/repository/releases") {
                authentication(userName: bawagNexusUser, password: bawagNexusPassword)
            }
            pom.version = version
            pom.artifactId = artifactId
            pom.groupId = group
        }
        mavenDeployer {
            repository(url: "$nexusUrl/repository/snapshots") {
                authentication(userName: bawagNexusUser, password: bawagNexusPassword)
            }
            pom.version = version
            pom.artifactId = artifactId
            pom.groupId = group
        }
    }
}
