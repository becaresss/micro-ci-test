#!groovyâ€‹

properties([[$class: 'BuildDiscarderProperty', strategy: [$class: 'LogRotator', numToKeepStr: '10']]])

stage('build & unit tests & nexus & sonar') {

    node('jenkins-slave') {
        checkout scm
        buildGradle()
    }
}

def branchType = getBranchType "${env.BRANCH_NAME}"
def branchDeploymentEnvironment = getBranchDeploymentEnvironment branchType

if (branchDeploymentEnvironment) {

    stage('build docker image') {

        node('jenkins-slave') {
            gradle "buildDockerImage pushDockerImage"
        }
    }

    stage('deploy artifact to DC/OS') {

        if (branchDeploymentEnvironment == "prod") {

            try {
                timeout(time: 1, unit: 'DAYS') {
                    input "Do you want to deploy to production?"
                }
            } catch (abort) {
                echo "Job finished due to ${abort}..."
                currentBuild.result = 'SUCCESS'
                return
            }

        }

        node('jenkins-slave') {
            deployArtifactGradle branchDeploymentEnvironment
        }
    }

    if (branchDeploymentEnvironment != "prod") {


        stage('perform tests') {

            node('jenkins-slave') {
                executeTestsGradle branchDeploymentEnvironment
            }
        }
    }

    if (branchType == "dev") {

        stage('start release') {

            try  {
                timeout(time: 1, unit: 'HOURS') {
                    input "Do you want to prepare a release process?"
                }

            } catch (abort) {
                echo "Job finished due to ${abort}..."
                currentBuild.result = 'SUCCESS'
                return
            }

            node('jenkins-slave') {
                gradle("initJGitflow")
                gradle("releaseStart")
                gradle("releasePublish")
            }
        }

    } else if (branchType == "release") {

        branchDeploymentEnvironment = "uat"

        stage('deploy release candidate to UAT') {

            try {

                timeout(time: 1, unit: 'HOURS') {
                    input 'Do you want to execute a release process? If so, after deployment and integration tests successfully passed against uat environment, release will be created...'
                }
            } catch (abort) {
                echo "Job finished due to ${abort}..."
                currentBuild.result = 'SUCCESS'
                return
            }

            node('jenkins-slave') {
                deployArtifactGradle branchDeploymentEnvironment
            }
        }

        stage('perform acceptation tests') {

            node('jenkins-slave') {
                executeTestsGradle branchDeploymentEnvironment
            }
        }

        stage('end release') {

            node('jenkins-slave') {
                def v = versionGradle()
                gradle("initJGitflow")
                sh "git checkout release/${v}"
                gradle("releaseFinish")
            }
        }

    } else if (branchType == "hotfix") {

        stage('finish hotfix') {

            try {
                timeout(time: 1, unit: 'HOURS') {
                    input "Is the hotfix finished?"
                }
            } catch (abort) {
                echo "Job finished due to ${abort}..."
                currentBuild.result = 'SUCCESS'
                return
            }

            node('jenkins-slave') {
                gradle("hotfixFinish")
            }
        }
    }
}


def getBranchType(String branchName) {

    def devPattern = ".*develop"
    def releasePattern = ".*release/.*"
    def featurePattern = ".*feature/.*"
    def hotfixPattern = ".*hotfix/.*"
    def masterPattern = ".*master"

    if (branchName =~ devPattern) {
        return "dev"
    } else if (branchName =~ releasePattern) {
        return "release"
    } else if (branchName =~ masterPattern) {
        return "master"
    } else if (branchName =~ featurePattern) {
        return "feature"
    } else if (branchName =~ hotfixPattern) {
        return "hotfix"
    } else {
        return null
    }
}

def getBranchDeploymentEnvironment(String branchType) {

    if (branchType == "dev") {
        return "dev"
    } else if (branchType == "release") {
        return "syt"
    } else if (branchType == "master") {
        return "prod"
    } else {
        return null
    }
}


def gradle(String tasks) {

    def gradleHome = tool "Gradle-4.9"

    withCredentials([usernamePassword(credentialsId: 'githubuser', usernameVariable: 'GIT_USER', passwordVariable: 'GIT_PASSWORD'),
                     string(credentialsId: 'nexusUrl', variable: 'NEXUS_URL'),
                     usernamePassword(credentialsId: 'nexus', usernameVariable: 'bawagNexusUser', passwordVariable: 'bawagNexusPassword'),
                     string(credentialsId: 'sonarUrl', variable: 'SONAR_URL')]) {

        withEnv(["PATH+GRADLE=${gradleHome}/bin"]) {
            sh "gradle ${tasks} -Pnexus.url=${NEXUS_URL} -PbawagNexusUser=${bawagNexusUser} -PbawagNexusPassword=${bawagNexusPassword} -PgitUsername=${GIT_USER} -PgitPassword=${GIT_PASSWORD} -Dsonar.host.url=${SONAR_URL}"
        }
    }
}

def versionGradle() {
    def props = readProperties  file: 'gradle.properties'
    return props['version']
}

def artifactIdGradle() {
    def props = readProperties  file: 'gradle.properties'
    return props['artifactId']
}

def buildGradle() {
    def v = versionGradle()
    currentBuild.displayName = "${env.BRANCH_NAME}-${v}-${env.BUILD_NUMBER}"
    gradle "clean unitTest assemble sonarqube uploadArchives"
    return false
}

def executeTestsGradle(String branchDeploymentEnvironment) {

    def artifactId = artifactIdGradle()

    echo "Waiting to Mesos DNS to register service..."
    sleep 30

    switch (branchDeploymentEnvironment) {

        case "dev":
            //Executing integration tests...
            gradle "clean integrationTest -Dremote.service.it.url=http://${artifactId}.marathon.mesos:8080"
            break
        case "syt":
            //Executing performance tests...
            gradle "clean processTestResources jmRun -Pjmeter.remoteServiceHost=${artifactId}.marathon.mesos -Pjmeter.remoteServicePort=8080"
            break
        case "uat":
            //Executing integration tests...
            gradle "clean integrationTest -Dremote.service.it.url=http://${artifactId}.marathon.mesos:8080"
            break
        default:
            break
    }
}


def deployArtifactGradle(String branchDeploymentEnvironment) {

    withCredentials([usernamePassword(credentialsId: 'jenkinsDcos_' + branchDeploymentEnvironment, usernameVariable: 'USER_ID', passwordVariable: 'USER_PASSWORD'),
                     string(credentialsId: 'dcosLoginUrl_' + branchDeploymentEnvironment, variable: 'DCOS_LOGIN_URL'),
                     string(credentialsId: 'marathonApiUrl_' + branchDeploymentEnvironment, variable: 'MARATHON_API_URL')]) {


        def artifactId = artifactIdGradle()

        withEnv(["ARTIFACT_ID=${artifactId}"]) {
            sh "echo Deploying to ${branchDeploymentEnvironment}"
            sh "/opt/dcos_deploy.sh"
        }
    }
}
